plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isShellApp = project.getName() == rootProject.shellApp
def isLaunchApp = project.getName() == rootProject.launchApp

if (isShellApp) {
    println "isShellApp: ${isShellApp}"
}

if (isLaunchApp) {
    println "isLaunchApp: ${isLaunchApp}"
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply from: "${rootDir}/config/buildConfigs.gradle"
apply from: "${rootDir}/config/optionConfigs.gradle"
apply from: "${rootDir}/config/featureConfigs.gradle"
apply from: "${rootDir}/config/versionConfigs.gradle"
apply from: "${rootDir}/config/signingConfigs.gradle"
apply from: "${rootDir}/config/libConfigs.gradle"

android {
    resourcePrefix 'comkit_'

    defaultConfig {
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            if (isLaunchApp) {
                shrinkResources feature.isMinifyEnabled
            }
        }

        release {
            if (isLaunchApp) {
                shrinkResources true
            }
        }
    }

    flavorDimensions 'library'
    productFlavors {
        if (isLaunchApp) {
            app {
                dimension 'library'
                applicationId 'com.githubyss.mobile.common.kit'
                versionCode 1
                versionName "1.0"
                manifestPlaceholders = [APP_NAME: "通用工具库"]
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                srcDirs 'src/main/kotlin'
            }
            res {
                srcDirs 'src/main/res'
                srcDirs 'src/main/res/common'
            }
            manifest {
                srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs {
                srcDirs 'libs'
            }
        }

        if (isLaunchApp) {
            app {
                java {
                    srcDirs 'src/app/java'
                    srcDirs 'src/app/kotlin'
                }
                res {
                    srcDirs 'src/app/res'
                    srcDirs 'src/app/res/common'

                    srcDirs 'src/app/res/homepage'
                    srcDirs 'src/app/res/binding_inline'
                    srcDirs 'src/app/res/binding_reflect'
                    srcDirs 'src/app/res/compose'
                    srcDirs 'src/app/res/json_utils'
                    srcDirs 'src/app/res/lifecycle'
                    srcDirs 'src/app/res/mvvm'
                    srcDirs 'src/app/res/mvi'
                }
                manifest {
                    srcFile 'src/app/AndroidManifest.xml'
                }
            }
        }

        test {
            java {
                srcDirs 'src/test/java'
                srcDirs 'src/test/kotlin'
            }
        }

        all {
            // set -> println "集合 ${set.name} 的文件是 ${set.java.srcDirs}"
        }
    }

    dataBinding {
        enabled feature.isDataBindingEnabled
    }

    buildFeatures {
        compose feature.isComposeEnabled
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':DesignPatternLibrary_Android')
    implementation project(':CommonResSdk_Android')
    implementation project(':CommonBaseSdk_Android')

    api dependenciesLib.androidx_appcompat
    api dependenciesLib.androidx_compose_material
    api dependenciesLib.androidx_activity_compose
    implementation dependenciesLib.google_gson
    implementation dependenciesLib.joda_time
    implementation dependenciesLib.bumptech_glide
    kapt dependenciesLib.bumptech_glide_compiler
    implementation dependenciesLib.alibaba_arouter_api

    debugImplementation dependenciesLib.leakcanary

    if (isLaunchApp) {
        appImplementation project(':DesignPatternLibrary_Android')

        appImplementation dependenciesLib.androidx_fragment_ktx
        appImplementation dependenciesLib.androidx_constraintlayout
        appImplementation dependenciesLib.androidx_recyclerview
        appImplementation dependenciesLib.google_flexbox
        appImplementation dependenciesLib.squareup_retrofit2
        appImplementation dependenciesLib.squareup_retrofit2_converter_moshi
    }
}
